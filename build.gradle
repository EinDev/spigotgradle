plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.11.0'
    id "com.palantir.git-version" version "0.12.3"
}

group 'com.gitlab.firestar99.spigotgradle'
def details = versionDetails()
def verPre = details.lastTag.substring(0, details.lastTag.lastIndexOf('.') + 1)
def verNumber = Integer.parseInt(details.lastTag.substring(details.lastTag.lastIndexOf('.') + 1, details.lastTag.length())) + details.commitDistance
def verPost = details.branchName == "master" ? "" : details.branchName ? "-" + details.branchName : "-detached"
version verPre + verNumber + verPost

gradlePlugin {
    plugins {
        SpigotBase {
            id = 'com.gitlab.firestar99.spigotgradle.base'
            displayName = 'Base plugin for SpigotGradle'
            description = 'Base plugin with shared code of SpigotGradle plugin and server'
            implementationClass = 'firestar99.spigotgradle.SpigotBasePlugin'
        }
        SpigotPlugin {
            id = 'com.gitlab.firestar99.spigotgradle.plugin'
            displayName = 'Setup spigot repos and dependencies for plugin development'
            description = 'This Plugin sets up the spigot repos and adds the spigot api dependency to the project automatically'
            implementationClass = 'firestar99.spigotgradle.plugin.SpigotPluginPlugin'
        }
        SpigotServer {
            id = 'com.gitlab.firestar99.spigotgradle.server'
            displayName = 'Setup a spigot server for development'
            description = 'Automatically builds the spigot jar using the spigot BuildTool, sets up a spigot server in /server and puts the plugins in the correct place'
            implementationClass = 'firestar99.spigotgradle.server.SpigotServerPlugin'
        }
    }
}

pluginBundle {
    website = 'https://gitlab.com/Firestar99/spigotgradle/readme.md'
    vcsUrl = 'https://gitlab.com/Firestar99/spigotgradle/'
    tags = ['minecraft', 'spigot', 'bukkit']
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

//noinspection GroovyUnusedAssignment
targetCompatibility = 1.8
//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:16.0.3'
    implementation localGroovy()

    testImplementation 'junit:junit:4.12'
}

publishPlugins.doFirst {
    if(!verPost.isEmpty())
        throw new GradleException("Only execute this on master! Otherwise versioning is messed up.");
}

plugins {
    id 'java-gradle-plugin'
    id "com.palantir.git-version" version "0.12.3"
}

group 'SpaceGameDev'
def details = versionDetails()
def verPre = details.lastTag.substring(0, details.lastTag.lastIndexOf('.') + 1)
def verNumber = Integer.parseInt(details.lastTag.substring(details.lastTag.lastIndexOf('.') + 1, details.lastTag.length())) + details.commitDistance
def verPost = details.branchName == "master" ? "" : details.branchName ? "-" + details.branchName : "-detached"
version verPre + verNumber + verPost

gradlePlugin {
    plugins {
        SpigotBase {
            id = 'spigot-base'
            implementationClass = 'spigot.gradle.SpigotBasePlugin'
        }
        SpigotPlugin {
            id = 'spigot-plugin'
            implementationClass = 'spigot.gradle.plugin.SpigotPluginPlugin'
        }
        SpigotServer {
            id = 'spigot-server'
            implementationClass = 'spigot.gradle.server.SpigotServerPlugin'
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

//noinspection GroovyUnusedAssignment
targetCompatibility = 1.8
//noinspection GroovyUnusedAssignment
sourceCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:16.0.3'
    implementation localGroovy()

    testImplementation 'junit:junit:4.12'
}
